*
* valid.mixal: check mix word for valid instruction
*
TERM	EQU	19
INST	EQU	1000
BUFG	EQU	INST+1
BUFB	EQU	BUFG+1
B	EQU	1(4:4)
BMAX	EQU	B-1
UMAX	EQU	20
TABLE	NOP	GOOD(BMAX)
	ADD	FLOAT(5:5)
	SUB	FLOAT(5:5)
	MUL	FLOAT(5:5)
	DIV	FLOAT(5:5)
	HLT	GOOD
	SRC	GOOD
	MOVE	MEMORY(BMAX)
	LDA	FLOAT(5:5)
	STZ	FLOAT(5:5)
	JBUS	MEMORY(BMAX)
	IOC	GOOD(UMAX)
	IN	MEMORY(UMAX)
	OUT	MEMORY(UMAX)
	JLE	MEMORY
	JANP	MEMORY
	JXNP	MEMORY
	ENNA	GOOD
	ENNX	GOOD
	CMPA	FLOAT(5:5)
	CMP1	FIELD(5:5)
	CMPX	FIELD(5:5)
BEGIN	JMP	SWORD
1H	LDA	INST
	CMPA	VALID(3:3)
	JG	BAD	I field > 6?
	LD1	INST(5:5)
	DEC1	64
	J1NN	BAD
	CMPA	TABLE+64,1(4:4)
	JG	BAD
	LD1	TABLE+64(1:2)
	JMP	0,1
FLOAT	CMPA	VALID(4:4)
	JE	MEMORY
FIELD	ENTA	0
	LDX	INST(4:4)
	DIV 	=9=
	STX	*+1(0:2)
	INCA	0
	DECA	5
	JAP	BAD
MEMORY	LDX	INST(3:3)
	JXNZ	GOOD
	LDX	INST(0:2)
	JXN	BAD
	DECX	3999
	JXNP	GOOD
	JMP	BAD
SWORD	STJ	EXIT	subroutine: store word in INST
	ENT1	1	C field for ADD instrution
	ST1	INST
EXIT	JMP	1B
GOOD	OUT	MSGG(TERM)
	HLT
BAD	OUT	MSGB(TERM)
	HLT
	ORIG	BUFG
MSGG	ALF	"GOOD "
	ORIG	BUFB
MSGB	ALF	"BAD  "
VALID	CMPX	3999,6(6)
	END	BEGIN
